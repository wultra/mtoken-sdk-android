/*
 * Copyright 2020 Wultra s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions
 * and limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        loadInstrumentationTestConfigProperties(project, owner)
    }

    buildTypes {
        debug {

        }
        release {
            minifyEnabled false
            consumerProguardFiles 'consumer-proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Bundled
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlinVersion}"
    implementation "androidx.annotation:annotation:${androidXAnnotations}"
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "com.jakewharton.threetenabp:threetenabp:${threetenabpVersion}"
    implementation "com.squareup.okhttp3:okhttp:${okHttpVersion}"

    // Dependencies
    compileOnly "com.wultra.android.powerauth:powerauth-sdk:${powerAuthSdkVersion}"
    compileOnly "io.getlime.core:rest-model-base:${restModelBaseVersion}"

    // TestDependencies
    testImplementation "junit:junit:${junitVersion}"

    // Android tests
    androidTestImplementation "com.wultra.android.powerauth:powerauth-sdk:${powerAuthSdkVersion}"
    androidTestImplementation "androidx.test:runner:${androidXTestVersion}"
    androidTestImplementation "junit:junit:${junitVersion}"
    androidTestImplementation "androidx.test:core:${androidXTestVersion}"
}

// Load properties for instrumentation tests.
static def loadInstrumentationTestConfigProperties(project, defaultConfig) {
    def configsRoot = new File("${project.rootProject.projectDir}/configs")
    def configPropertiesFile = new File(configsRoot, "integration-tests.properties")

    def instrumentationArguments = [
            "tests.sdk.paServerUrl",
            "tests.sdk.nextStepServerUrl",
            "tests.sdk.enrollmentServerUrl",
            "tests.sdk.operationsServerUrl",
            "tests.sdk.appKey",
            "tests.sdk.appSecret",
            "tests.sdk.masterServerPublicKey",
            "tests.sdk.appId"
    ]

    project.logger.info("LOADING_PROPERTIES Reading $configPropertiesFile")
    if (configPropertiesFile.canRead()) {
        def props = new Properties()
        props.load(new FileInputStream(configPropertiesFile))

        for (key in instrumentationArguments) {
            defaultConfig.testInstrumentationRunnerArgument key, "${props[key]}"
        }
    } else {
        project.logger.warn("Loading properties error: Missing $configPropertiesFile")
    }
}

apply from: 'android-release-aar.gradle'